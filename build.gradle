plugins {
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.0.14'
    // zipの"image/lib/modules"内にJDKとOpenJFXとアプリが合体して保存される
    id 'org.beryx.jlink' version '2.26.0'
    // JDKのみにしたい場合は"org.beryx.runtime"を使う
}

group 'info.repy'
version '1.0-SNAPSHOT'

java{
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

application {
    mainModule = 'info.repy.adoptopenjdkupdate'
    mainClass = 'info.repy.adoptopenjdkupdate.Main'
}

compileJava {
    options.encoding = "UTF-8"
}

javafx {
    modules = ['javafx.controls', 'javafx.fxml']
}

jlink {
    // zipの"image/lib/modules"内にJDKとOpenJFXとアプリが合体して保存される
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages', '--add-modules', 'java.base,jdk.crypto.cryptoki,jdk.crypto.ec']
    launcher {
        name = 'openjdkdownloader'
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.json:json:20230227'
    implementation 'org.apache.commons:commons-compress:1.21'
    compileOnly 'org.projectlombok:lombok:1.18.28'
    annotationProcessor 'org.projectlombok:lombok:1.18.28'
    testCompileOnly 'org.projectlombok:lombok:1.18.28'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.28'
}

task buildWindows(type: Zip, dependsOn: jlinkZip) {
    archiveFileName = "openjdkupdate.zip"
    from zipTree(tasks.jlinkZip.imageZip)
    from "scripts/windows/openjdkdownloader.bat"
}

task buildMacDir(dependsOn: jlinkZip) {
    doLast {
        delete fileTree('build/macapp')
        copy {
            from fileTree("scripts/mac")
            into 'build/macapp/OpenJDKUpdate.app'
        }
        copy {
            from zipTree(tasks.jlinkZip.imageZip)
            into 'build/macapp/OpenJDKUpdate.app/Contents/MacOS'
        }
    }
}

task buildMac(type: Tar, dependsOn: buildMacDir) {
    compression = Compression.GZIP
    archiveFileName = "openjdkupdate.tar.gz"
    from 'build/macapp'
}

task buildLinux(type: Tar, dependsOn: jlinkZip) {
    compression = Compression.GZIP
    archiveFileName = "openjdkupdate.tar.gz"
    from zipTree(tasks.jlinkZip.imageZip)
    from "scripts/linux/openjdkdownloader.sh"
}
